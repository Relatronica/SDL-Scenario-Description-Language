// ============================================================
// SDL Example: Sorveglianza Biometrica — Rischio e Regolazione 2025-2035
// ============================================================
// Evoluzione della sorveglianza biometrica AI in Europa:
// diffusione, incidenti, pressione regolatoria, diritti civili.
// Basato su AI Act Art. 5-6 e GDPR Art. 9, 22.
// ============================================================

scenario "Sorveglianza Biometrica — Rischio e Regolazione" {
  timeframe: 2025 -> 2035
  resolution: yearly
  confidence: 0.30
  author: "Relatronica — Citizen Lab"
  version: "1.0"
  description: "Evoluzione della sorveglianza biometrica AI in Europa: diffusione, incidenti, pressione regolatoria, diritti civili. Basato su AI Act Art. 5-6 e GDPR Art. 9, 22."
  tags: ["biometrica", "sorveglianza", "ai-act", "gdpr", "diritti-civili"]
  subtitle: "Riconoscimento facciale, privacy e diritti nell'UE"
  category: regolamentazione
  icon: "eye"
  color: "#7c3aed"
  difficulty: avanzato

  // ── Assumptions ──

  assumption facial_recognition_accuracy {
    value: 0.92
    source: "NIST FRVT 2024"
    confidence: 0.70
    uncertainty: beta(10, 1)
  }

  assumption cctv_density_eu {
    value: 35
    source: "Comparitech 2024 — avg 35 cameras per 1000 people"
    confidence: 0.55
    uncertainty: normal(±20%)
  }

  // ── Parameters ──

  parameter regulatory_strictness {
    value: 70
    range: [20, 100]
    step: 5
    label: "Rigore regolatorio"
    unit: "indice"
    source: "AI Act Art. 5 prohibition; national implementation surveys"
    format: "{value}/100"
    control: slider
    description: "Livello di rigore con cui gli stati UE applicano il divieto di sorveglianza biometrica real-time (Art. 5)"
  }

  parameter exception_scope {
    value: 15
    range: [5, 50]
    step: 5
    label: "Ampiezza eccezioni"
    unit: "indice"
    source: "AI Act Art. 5(1)(h) exceptions analysis"
    format: "{value}/100"
    control: slider
    description: "Quanto ampiamente gli stati interpretano le eccezioni al divieto (terrorismo, persone scomparse, etc.)"
  }

  parameter tech_investment {
    value: 4.5B EUR
    range: [1B EUR, 12B EUR]
    step: 0.5B EUR
    label: "Investimento tecnologie biometriche"
    unit: "miliardi EUR"
    source: "EU biometric market forecast 2025; MarketsAndMarkets"
    format: "{value} mld €"
    control: slider
    description: "Investimento annuo europeo in tecnologie di identificazione biometrica"
  }

  parameter civil_society {
    value: 65
    range: [20, 95]
    step: 5
    label: "Pressione società civile"
    unit: "indice"
    source: "EDRi, Reclaim Your Face, ECI campaigns; Eurobarometer"
    format: "{value}/100"
    control: slider
    description: "Forza della pressione da ONG, movimenti civili e opinione pubblica contro la sorveglianza"
  }

  // ── Variables ──

  variable deployed_systems {
    description: "Sistemi biometrici AI attivi nell'UE"
    unit: "sistemi"
    label: "Sistemi attivi"
    icon: "camera"
    color: "#8b5cf6"

    2025: 4200
    2026: 3800
    2027: 3200
    2028: 2800
    2029: 2600
    2031: 2200
    2033: 2000
    2035: 1900

    depends_on: regulatory_strictness, exception_scope, tech_investment, civil_society
    uncertainty: normal(±18%)
    interpolation: spline
  }

  variable false_ids {
    description: "Identificazioni errate con conseguenze legali (annue)"
    unit: "casi"
    label: "Falsi positivi gravi"
    icon: "alert-triangle"
    color: "#ef4444"

    2025: 850
    2026: 720
    2027: 580
    2028: 450
    2029: 380
    2031: 250
    2033: 180
    2035: 120

    depends_on: regulatory_strictness, exception_scope, tech_investment, civil_society
    uncertainty: lognormal(5.5, 0.5)
    interpolation: spline
  }

  variable privacy_complaints {
    description: "Reclami formali alle DPA per violazioni biometriche"
    unit: "reclami"
    label: "Reclami privacy"
    icon: "clipboard-list"
    color: "#f59e0b"

    2025: 12000
    2026: 18000
    2027: 25000
    2028: 32000
    2029: 28000
    2031: 22000
    2033: 16000
    2035: 12000

    depends_on: regulatory_strictness, exception_scope, tech_investment, civil_society
    uncertainty: lognormal(9, 0.4)
    interpolation: spline
  }

  variable sanctions_bio {
    description: "Sanzioni per violazione Art. 5 (cumulativo)"
    unit: "milioni EUR"
    label: "Sanzioni Art. 5"
    icon: "coins"
    color: "#dc2626"

    2025: 0
    2026: 15
    2027: 85
    2028: 220
    2029: 380
    2031: 550
    2033: 680
    2035: 750

    depends_on: regulatory_strictness, exception_scope, tech_investment, civil_society
    uncertainty: lognormal(4.5, 0.6)
    interpolation: spline
  }

  variable civil_liberties {
    description: "Indice percezione diritti civili digitali (0-100)"
    unit: "indice"
    label: "Diritti civili digitali"
    icon: "shield"
    color: "#3b82f6"

    2025: 62
    2026: 60
    2027: 63
    2028: 67
    2029: 70
    2031: 74
    2033: 77
    2035: 80

    depends_on: regulatory_strictness, exception_scope, tech_investment, civil_society
    model: sigmoid(k=0.25, midpoint=2029)
    uncertainty: beta(5, 4)
    interpolation: spline
  }

  variable compliant_market {
    description: "Fatturato mercato biometrico conforme (non real-time, registrato)"
    unit: "miliardi EUR"
    label: "Mercato conforme"
    icon: "check"
    color: "#10b981"

    2025: 1.2
    2026: 1.8
    2027: 2.8
    2028: 4.0
    2029: 5.5
    2031: 7.8
    2033: 9.5
    2035: 11.2

    depends_on: regulatory_strictness, exception_scope, tech_investment, civil_society
    model: logistic(k=0.4, midpoint=2030)
    uncertainty: lognormal(1.5, 0.3)
    interpolation: spline
  }

  // ── Impacts ──

  impact erosione_diritti {
    description: "Erosione diritti civili digitali rispetto al baseline 2025"
    unit: "punti"
    label: "Erosione diritti"
    icon: "zap"
    color: "#ef4444"
    derives_from: civil_liberties
    formula: 62 - civil_liberties
  }

  impact efficacia_enforcement {
    description: "Riduzione sistemi biometrici rispetto al 2025"
    unit: "percentuale"
    label: "Efficacia enforcement"
    icon: "bar-chart"
    color: "#10b981"
    derives_from: deployed_systems
    formula: (4200 - deployed_systems) / 4200 * 100
  }

  impact costo_sociale_errori {
    description: "Costo sociale stimato dei falsi positivi"
    unit: "milioni EUR"
    label: "Costo sociale errori"
    icon: "banknote"
    color: "#f59e0b"
    derives_from: false_ids
    formula: false_ids * 0.025
  }

  // ── Branches ──

  branch "Eccezioni Allargate" when exception_scope > 35 {
    probability: 0.18

    variable deployed_systems {
      2028: 4200
      2031: 5060
      2035: 4940
      uncertainty: normal(±20%)
    }

    variable civil_liberties {
      2028: 55
      2031: 45
      2035: 40
      uncertainty: beta(3, 6)
    }
  }

  branch "Divieto Totale Effettivo" when regulatory_strictness > 90 {
    probability: 0.15

    variable deployed_systems {
      2027: 1504
      2029: 494
      2031: 198
      2035: 57
      uncertainty: normal(±30%)
    }

    variable civil_liberties {
      2029: 80
      2031: 85
      2035: 92
      uncertainty: beta(7, 2)
    }
  }

  branch "Evento Sicurezza Catalizzatore" when exception_scope > 25 {
    probability: 0.10

    variable deployed_systems {
      2029: 5460
      2031: 7040
      2035: 5510
      uncertainty: normal(±25%)
    }

    variable civil_liberties {
      2029: 48
      2031: 42
      2035: 50
      uncertainty: beta(3, 5)
    }
  }

  // ── Simulation ──

  simulate {
    runs: 2000
    method: monte_carlo
    seed: 42
    output: distribution
    percentiles: [5, 25, 50, 75, 95]
    convergence: 0.01
  }
}
